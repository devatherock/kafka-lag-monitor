plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.micronaut.application' version '3.7.10'
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'org.sonarqube' version '5.0.0.4638'
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id 'com.diffplug.spotless' version '6.25.0'
}

version = "0.3.0"
group = "io.github.devatherock"
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
}

micronaut {
    runtime("netty")
    processing {
        incremental(true)
        annotations("io.github.devatherock.*")
    }
}

configurations {
    // Force dependency versions to use dependencies without vulnerabilities
    all {
        resolutionStrategy {
            force 'org.jsoup:jsoup:1.17.2'
            force 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
        }
    }
}

dependencies {
    def lombokVersion = '1.18.32'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'
    compileOnly("org.graalvm.nativeimage:svm")

    implementation("io.micronaut:micronaut-inject")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.swagger.core.v3:swagger-annotations")
    implementation("io.micronaut:micronaut-management")
    implementation("io.micronaut.micrometer:micronaut-micrometer-core")
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-influx")
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-prometheus")

    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.4'
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '3.7.0'
    implementation group: 'org.xerial.snappy', name: 'snappy-java', version: '1.1.10.5'
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'
    implementation("ch.qos.logback:logback-classic:1.5.6")

    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-3.0'
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.14.17'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.4'
    testImplementation group: 'io.micronaut', name: 'micronaut-inject-groovy'
    testImplementation group: 'io.micronaut.test', name: 'micronaut-test-spock'
    testImplementation group: 'org.influxdb', name: 'influxdb-java', version: '2.24'
}

application {
    mainClass.set("io.github.devatherock.Application")
}

test {
    doFirst {
        if (Boolean.getBoolean('graalvm')) {
            jvmArgs("-agentlib:native-image-agent=access-filter-file=$projectDir/src/main/resources/graal/access-filter.json,config-output-dir=$projectDir/build/graal")
        }
    }
}

// Generates resource-config.json file for graalvm
build.dependsOn(generateResourcesConfigFile)

tasks.withType(Test) {
    useJUnitPlatform()
    environment('DOCKER_NETWORK_IP', System.getenv('DOCKER_NETWORK_IP') ?: 'localhost')
}

run {
    classpath += configurations.developmentOnly
    jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
    systemProperties(System.properties)
}

ext.jacoco = [
    exclusions: [
        'io/github/devatherock/Application.class'
    ],
    coverageThresholds: [
        'io.github.devatherock.logback.LogbackConfigInitializer': [
            'BRANCH': 0.75,
            'COMPLEXITY': 0.62,
            'INSTRUCTION': 0.89,
            'LINE': 0.88
        ],
    ],
]

apply from: "${getPropertyValue('gradle.includes.location', 'https://raw.githubusercontent.com/devatherock/gradle-includes/master')}/integration.gradle"
apply from: "${getPropertyValue('gradle.includes.location', 'https://raw.githubusercontent.com/devatherock/gradle-includes/master')}/checks.gradle"

/**
 * Fetches the value of a gradle property
 *
 * @param propertyName
 * @param propertyValueDefault
 * @return the property value
 */
String getPropertyValue(String propertyName, String propertyValueDefault = null) {
    return project.hasProperty(propertyName) ? project[propertyName] : propertyValueDefault
}