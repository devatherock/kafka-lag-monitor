plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.micronaut.application' version '4.5.0'
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'org.sonarqube' version '6.0.1.5171'
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id 'com.diffplug.spotless' version '7.0.2'
}

version = "2.0.0"
group = "io.github.devatherock"
sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
}

micronaut {
    runtime("netty")
    processing {
        incremental(true)
        annotations("io.github.devatherock.*")
    }
}

configurations {
    // Force dependency versions to use dependencies without vulnerabilities
    all {
        resolutionStrategy {
            force 'org.jsoup:jsoup:1.19.1'
            force 'com.fasterxml.jackson.core:jackson-databind:2.18.3'
        }
    }
}

dependencies {
    def lombokVersion = '1.18.36'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    annotationProcessor("io.micronaut.validation:micronaut-validation-processor")

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'
    compileOnly("org.graalvm.nativeimage:svm")

    implementation("io.micronaut:micronaut-inject")
    implementation("io.micronaut.validation:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.swagger.core.v3:swagger-annotations")
    implementation("io.micronaut:micronaut-management")
    implementation("io.micronaut.micrometer:micronaut-micrometer-core")
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-influx")
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-prometheus")

    // Micronaut 4.x: To fix 'No bean of type [io.micronaut.json.JsonMapper] exists'
    implementation("io.micronaut:micronaut-jackson-databind")

    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.0'
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '4.0.0'
    implementation group: 'org.xerial.snappy', name: 'snappy-java', version: '1.1.10.7'
    implementation("ch.qos.logback:logback-classic:1.5.18")

    // Micronaut 4.x: To fix 'YAML configuration file detected but snakeyaml is not on classpath'
    runtimeOnly("org.yaml:snakeyaml")

    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-4.0'
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.17.3'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.4'
    testImplementation group: 'io.micronaut', name: 'micronaut-inject-groovy'
    testImplementation group: 'io.micronaut.test', name: 'micronaut-test-spock'
    testImplementation group: 'org.influxdb', name: 'influxdb-java', version: '2.24'
}

application {
    mainClass.set("io.github.devatherock.Application")
}

tasks.withType(Test) {
    useJUnitPlatform()
    environment('DOCKER_NETWORK_IP', System.getenv('DOCKER_NETWORK_IP') ?: 'localhost')
    environment('LOGGING_CONFIG', System.getenv('LOGGING_CONFIG') ?: '')
}

run {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
    systemProperties(System.properties)
}

ext.jacoco = [
    exclusions: [
        'io/github/devatherock/Application.class'
    ],
    coverageThresholds: [
        'io.github.devatherock.logback.LogbackConfigInitializer': [
            'BRANCH': 0.75,
            'COMPLEXITY': 0.62,
            'INSTRUCTION': 0.89,
            'LINE': 0.88
        ],
    ],
]

apply from: "${getPropertyValue('gradle.includes.location', 'https://raw.githubusercontent.com/devatherock/gradle-includes/master')}/native.gradle"
apply from: "${getPropertyValue('gradle.includes.location', 'https://raw.githubusercontent.com/devatherock/gradle-includes/master')}/integration.gradle"
apply from: "${getPropertyValue('gradle.includes.location', 'https://raw.githubusercontent.com/devatherock/gradle-includes/master')}/checks.gradle"

/**
 * Fetches the value of a gradle property
 *
 * @param propertyName
 * @param propertyValueDefault
 * @return the property value
 */
String getPropertyValue(String propertyName, String propertyValueDefault = null) {
    return project.hasProperty(propertyName) ? project[propertyName] : propertyValueDefault
}
