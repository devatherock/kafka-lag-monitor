plugins {
    id("com.github.johnrengelman.shadow") version "6.1.0"
    id("io.micronaut.application") version "1.3.2"
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'org.sonarqube' version '3.0'
    id 'com.github.kt3k.coveralls' version '2.10.2'
}

version = "0.1.0"
group = "io.github.devatherock"

repositories {
    mavenCentral()
    jcenter()
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("io.github.devatherock.*")
    }
}

dependencies {
    def lombokVersion = '1.18.16'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    compileOnly("org.graalvm.nativeimage:svm")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.swagger.core.v3:swagger-annotations")
    implementation("io.micronaut:micronaut-management")
    implementation("io.micronaut.micrometer:micronaut-micrometer-core")
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-influx")
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-prometheus")
    runtimeOnly("ch.qos.logback:logback-classic")

    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '2.6.1'

    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.1'
}

application {
    mainClass.set("io.github.devatherock.Application")
}

run {
    classpath += configurations.developmentOnly
    jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
    systemProperties(System.properties)
}

ext.jacoco = [
    exclusions: [
        'io/github/devatherock/Application.class',
        'io/github/devatherock/test/InfluxdbController.class',
        
        // TODO Remove exclusions once tests have been written
        'io/github/devatherock/config/AppConfig.class',
        'io/github/devatherock/config/ApplicationProperties*'
    ],
    // TODO Remove once all tests have been written
    coverageThresholds: [
        'io.github.devatherock.scheduler.KafkaLagCollector': [
            'BRANCH': 0.50,
            'COMPLEXITY': 0.46,
            'INSTRUCTION': 0.57,
            'LINE': 0.64
        ]
    ]
]
apply from: '../gradle-includes/checks.gradle'